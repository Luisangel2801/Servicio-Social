---
switcher-label: Placa
---

# Sensor MQ

## Descripción

Los sensores MQ son sensores de gas de bajo costo y fácil uso que permiten detectar la presencia de diferentes gases en el ambiente. El sensor MQ135, en particular, es capaz de detectar gases como amoníaco $(NH_3)$, benceno $(C_6H_6)$, alcohol, monoxido de carbo $(CO)$ y dióxido de carbono $(CO_2)$.

## Tipos comunes de sensores MQ

- **MQ-2:** Detecta gases inflamables y humo.
- **MQ-3:** Detecta alcohol, etanol y humo.
- **MQ-4:** Detecta gas metano y gas natural.
- **MQ-5:** Detecta gas natural.
- **MQ-6:** Detecta gas de propano.
- **MQ-7:** Detecta monóxido de carbono.
- **MQ-8:** Detecta gas de hidrógeno.
- **MQ-9:** Detecta gas de monóxido de carbono y gases combustibles.
- **MQ-135:** Detecta amoniaco, benceno, CO y CO₂.

## Características del sensor MQ 135

- **Rango de detección de gases:** Amoniaco, Benceno, Vapor de Alcohol y Humo.
- **Voltaje de operación:** 5 V
- **Tiempo de calentamiento:** 24 horas
- **Sensibilidad:** ajustable
- **Rango de detección:** 10 a 1000 ppm
- **Temperatura de operación:** -10 a 50 °C

## Pinout del sensor MQ 135

El sensor MQ 135 tiene 4 pines, los cuales son:

- `A0`: Salida analógica.
- `D0`: Salida digital.
- `GND`: Conexión a tierra.
- `VCC`: Conexión a 5 V.

![MQ135](MQ135.png) {border-effect="rounded" width="500" }

## Calibrar el sensor

Los sensores MQ son electroquímicos y varían su resistencia cuando se exponen a determinados gases, internamente posee un calentador encargado de aumentar la temperatura interna y con esto el sensor pueda reaccionar con los gases provocando un cambio en el valor de la resistencia. El calentador dependiendo del modelo puede necesitar un voltaje entre 5 y 2 volts, el sensor necesita una resistencia de carga $(R_L)$ para cerrar el circuito y con este hacer un divisor de voltaje y obtener la relación de voltaje $(\frac{RS}{RO})$ en aire limpio.

> Antes de calibrar, conecte el sensor a una fuente de 5 V y esperar un tiempo de calentamiento para que la salida sea estable y tenga las características que el fabricante muestra en sus [datasheet](https://uelectronics.com/wp-content/uploads/2018/01/SNS-MQ135.pdf "Link al datasheet del sensor MQ135"), dicho tiempo dependiendo del modelo puede ser entre 12 y 48 horas.


### Calibración de la resistencia de carga $R_L$
Para calibrar el sensor **MQ135**, primero es necesario determinar el valor de la resistencia de carga $R_L$, para esto se puede seguir los siguientes pasos:

<procedure title="Pasos para obtener el valor de la resistencia de carga RL" id="resistencia-RL">
    <step>Tomar el sensor MQ135 y volterarlo para ver la parte posterior donde se encuentra el potenciómetro.</step>
    <img src="MQ135_potenciometro.jpg" border-effect="rounded" width="500" alt="Mq135_potenciometro"/>
    <step>Poner la punta negativa del multímetro en el pin de GND y la punta positiva en el pin de A0.</step>
    <step>Medir la resistencia del sensor y anotar el valor. En este caso, se obtuvo un valor de 750 Ω.</step>
</procedure>

### Calibración de la resistencia $R_O$ en aire limpio

Una vez obtenido el valor de la resistencia $RL$ debemos obtener el valor de $RO$ en aire limpio, para esto se puede seguir los siguientes pasos:

<procedure title="Pasos para obtener el valor de la resistencia RO en aire limpio" id="resistencia-RO">
    <step>Conectar el sensor MQ135 a una fuente de 5 V y esperar un tiempo de calentamiento.</step>
    <step>Estimar el valor de <math>\frac{RS}{RO}</math> en aire limpio, con ayuda de la curva característica del sensor MQ135. En este caso se obtuvo un valor aproximado de  <math>3.8 </math> </step>
    <warning>
        Es importante tener en cuenta que la curva característica del sensor MQ135 es representada de manera logarítmica.
    </warning>
    <img src="MQ135-curva.jpg" border-effect="rounded" width="500" alt="Mq135_curva"/>
    <step> Conectar el sensor a la placa de desarrollo y subir el siguiente código para obtener el valor de la resistencia RO en aire limpio.</step>
    <note>
        Realizar la conexión a la placa como se puede ver en el esquema de conexión. Dejar correr el código en un lugar con aire limpio hasta obtener un valor estable para la resistencia RO. Es importante tener en cuenta que el valor de la resistencia RO puede variar dependiendo de la temperatura y humedad del ambiente.
    </note>
    <tabs>
        <tab title="main.cpp">
            <code-block lang="c++">
                #include &lt;Arduino.h&gt;
                &emsp;
                #ifdef ESP8266_BOARD
                    const int analog_PIN = A0;          // Pin analógico del ESP8266
                    const int adc_resolution = 1024 - 1;// Resolución ADC de 10 bits
                #elif defined(ESP32_BOARD)
                    const int analog_PIN = 34;          // Pin analógico del ESP32
                    const int adc_resolution = 4096 - 1;// Resolución ADC de 12 bits
                #endif
                &emsp;
                float VCC = 5.0; // Voltaje de alimentación del sensor
                float clean_air_factor = 3.85; // Relación de voltaje en aire limpio
                &emsp;
                void setup() {
                    Serial.begin(115200);
                }
                &emsp;
                void loop() {
                    float RS_air;
                    float RO;
                    float avgReadings = 0;
                    &emsp;
                    // Lectura del sensor en el puerto analógico
                    for (int i = 0; i &lt; 100; i++){
                        avgReadings = avgReadings + analogRead(analog_PIN);
                    }
                    avgReadings = avgReadings / 100.0;  // promedio de 100 lecturas
                    &emsp;
                    // Conversión de lectura a voltaje
                    float voltage = avgReadings * (VCC / adc_resolution);
                    &emsp;
                    // Cálculo de la resistencia RS en aire limpio
                    RS_air = (VCC / voltage - 1);
                    RO = RS_air/clean_air_factor;
                    &emsp;
                    Serial.print(&quot;sensor_volt = &quot;);
                    Serial.print(voltage);
                    Serial.print(&quot;\t&quot;);
                    Serial.print(&quot;R0 = &quot;);
                    Serial.println(RO);
                    delay(1000);
                }
            </code-block>
        </tab>
        <tab title="platformio.ini">
            <code-block lang="Plain Text">
                [env:esp32]
                platform = espressif32
                board = esp32dev
                framework = arduino
                monitor_speed = 115200
                build_flags = -D ESP32_BOARD
                &emsp;
                [env:esp8266]
                platform = espressif8266
                board = esp12e
                framework = arduino
                monitor_speed = 115200
                build_flags = -D ESP8266_BOARD
            </code-block>
        </tab>
    </tabs>
    <step> Una vez obtenido el valor de la resistencia RO en aire limpio, es importante guardarlo para el cálculo de la concentración de <math>CO_2</math> en el aire.</step>
</procedure>

### Cálculo de la concentración de CO₂


<procedure title="Pasos para calcular la concentración de CO₂ en el aire" id="concentracion-CO2">
    <step> Obtener dos puntos de la curva característica del gas <math>CO_2</math>. En este caso, obtuvimos los siguientes valores <math>Y_1 = 1.4, X_1 = 10, Y_2 = 1.1, X_2 = 100 </math>.</step>
    <step> Sustituir los valores obtenidos en la ecuación 1 </step>
    <code-block lang="tex">
        \begin{equation}
            log(x) = \frac{(log(Y)-log(Y_1)) \cdot \frac{X_2}{X_1}}{log(Y_2)-log(Y_1)} + log(X_1)
        \end{equation}
    </code-block>
    <step> Realizar el cálculo para obtener los coeficientes de la ecuación de correlación: </step>
    <code-block lang="tex">
        \begin{equation}
            log(x) = -9.54745 \cdot log(Y) + 2.39597
        \end{equation}
    </code-block>
    <step> Despejar x de la ecuación de correlación: </step>
    <code-block lang="tex">
        \begin{equation}
            x = 116.6020682 \cdot (RS/RO)^{-2.769034857}
        \end{equation}
    </code-block>
    <step> Obtener <math>RS</math> por división de voltaje: </step>
    <code-block lang="tex">
        \begin{equation}
            RS = (\frac{VCC}{VoltageLeido} - 1) \cdot RL
        \end{equation}
    </code-block>
</procedure>

> Los pasos anteriores son necesarios para obtener la concentración de cualquier gas que se desee detectar, en este caso se realizó el cálculo para el gas $CO_2$. Los valores de los coeficientes de la ecuación de correlación se obtuvieron de la hoja de datos del sensor MQ135. Se puede ver la aplicación de estos pasos en el [código del proyecto](#code_MQ135-id).
> {style="note"}


## Esquema de conexión para el ESP8266 {switcher-key="ESP8266"}

El sensor se conecta a la placa de desarrollo ESP8266 en el pin analógico A0. Se le agregó un led en el pin digital del sensor para indicar la alerta de calidad del aire. Es importanté conectar el positivo del led a una resistencia con dirección a VCC, esto debido a que cuando el sensor detecte un nivel de $CO_2$ peligroso, el nivel lógico del pin digital será 0 y el led se encenderá.

![MQ135_ESP8266](MQ135_ESP8266.png) {border-effect="rounded" width="500" thumbnail="true"}

## Esquema de conexión para el ESP32 {switcher-key="ESP32"}

El sensor se conecta a la placa de desarrollo ESP8266 en el pin analógico D34. Se le agregó un led en el pin digital del sensor para indicar la alerta de calidad del aire. Es importanté conectar el positivo del led a una resistencia con dirección a VCC, esto debido a que cuando el sensor detecte un nivel de $CO_2$ peligroso, el nivel lógico del pin digital será 0 y el led se encenderá.

![MQ135_ESP32](MQ135_ESP32.png) {border-effect="rounded" width="500" thumbnail="true"}


## Código {id="code_MQ135-id"}

El siguiente código permite medir la concentración de $CO_2$ en el aire con el sensor MQ135 y evaluar la calidad del aire en función de la concentración de $CO_2$. El código se puede cargar en una placa de desarrollo ESP8266 o ESP32.

> Sustituír el valor de la resistencia $RO$ y $RL$ obtenidos en los pasos anteriores en el código.
> {style="warning"}

<tabs>
    <tab title="main.cpp">
        <code-block lang="c++">
        /*
         * MQ135 - Sensor de calidad de aire
         * Autor: Luis Ángel Cruz Díaz
         * Fecha:  17/11/2023
         *
         * Programa para medir la concentración de CO2 en el aire con un 
         * sensor MQ135 y evaluar la calidad del aire en función de la 
         * concentración de CO2. Este programa utiliza la curva característica 
         * del sensor MQ135 para calibrar el sensor y obtener la concentración 
         * de CO2 en partes por millón (ppm).
         * 
         * El sensor MQ135 se conecta con el ESP8266 de la siguiente manera:
         * 
         * ESP8266      MQ135       Power Supply
         * A0-----------A0
         * GND----------GND---------GND
         *              VCC---------5V
         * 
         * El sensor MQ135 se conecta con el ESP32 de la siguiente manera:
         * 
         * ESP32        MQ135       Power Supply
         * D34----------A0
         * GND----------GND---------GND
         *              VCC---------5V
         */
        &emsp;
        #include &lt;Arduino.h&gt;
        &emsp;
        #ifdef ESP8266_BOARD
            const int analog_PIN = A0;          // Pin analógico del ESP8266
            const int adc_resolution = 1024 - 1;// Resolución ADC de 10 bits
        #elif defined(ESP32_BOARD)
            const int analog_PIN = 34;          // Pin analógico del ESP32
            const int adc_resolution = 4096 - 1;// Resolución ADC de 12 bits
        #endif
        &emsp;
        const float VCC = 5;    // Voltaje de alimentación del sensor MQ135
        const float RL = 0.750; // Resistencia de carga del sensor MQ135
        const float air_factor = 3.8;// Factor de aire limpio (RS/Ro)
        float RO = 7.70;        // Resistencia del sensor en aire limpio
        &emsp;
        float temperature = 25; // Temperatura en grados Celsius
        float humidity = 50;    // Humedad relativa en porcentaje
        &emsp;
        #define A 116.6020682   // Coeficiente A de la ecuación de correlación
        #define B 2.769034857   // Coeficiente B de la ecuación de correlación
        &emsp;
        #define CORA .00035
        #define CORB .02718
        #define CORC 1.39538
        #define CORD .0018
        #define CORE -.003333333
        #define CORF -.001923077
        #define CORG 1.130128205
        &emsp;
        String evaluateAirQuality(float ppm) {
            if (ppm &lt; 400) return &quot;Excelente&quot;;
            else if (ppm &lt; 1000) return &quot;Buena&quot;;
            else if (ppm &lt; 2000) return &quot;Moderada&quot;;
            else if (ppm &lt; 5000) return &quot;Mala&quot;;
            else return &quot;Peligrosa&quot;;
        }
        &emsp;
        // Función para obtener el factor de corrección
        float getCorrectionFactor(float tem, float hum) {
            if(tem &lt; 20){
                return CORA * tem * tem - CORB * tem + CORC - (hum-33.)*CORD;
            } else {
                return CORE * tem + CORF * hum + CORG;
            }
        }
        &emsp;
        // Función para obtener la resistencia del sensor MQ135
        float getRS(int samples){
            float avgReadings = 0;
            // Leer el sensor en el puerto analógico y obtener el promedio
            for (int i = 0; i &lt; samples; i++) {
                avgReadings += analogRead(analog_PIN);  
                delay(10);
            }
            avgReadings /= samples;
            // Conversión de lectura a voltaje
            float voltage = avgReadings * (VCC / adc_resolution);
            // Cálculo de resistencia RS
            float RS = (VCC / voltage - 1) * RL;
            return RS;
        }
        &emsp;
        // Función para calibrar la resistencia RO
        void calibrateRO() {
            RO = getRS(10000) / air_factor;
            Serial.printf(&quot;RO calibrado: %.2f Ω\n&quot;, RO);
        }
        &emsp;
        // Función para leer la concentración de CO2 en ppm
        float readCO2PPM() {
            float RS = getRS(100);  // Obtener la resistencia RS
            // Corregir la resistencia RS
            float RSCorrected = RS / getCorrectionFactor(temperature, humidity);
            // Calcular la concentración en ppm con la ecuación de correlación
            float ppm = A * pow((RSCorrected/RO), -B); 
            return ppm;
        }
        &emsp;
        void setup() {
            Serial.begin(115200);
            Serial.println(&quot;Iniciando sensor MQ135...&quot;);
            delay(10000);
            Serial.println(&quot;Calibrando sensor MQ135...&quot;);
            //calibrateRO();
        }
        &emsp;
        void loop() {
            float ppm = readCO2PPM();
            if (ppm &gt; 0) {
                Serial.printf(&quot;Concentración de CO2: %.2f ppm\n&quot;,ppm);
                String airQuality = evaluateAirQuality(ppm);
                Serial.printf(&quot;Calidad del aire: %s\n&quot;, airQuality.c_str());
            }
            Serial.println(&quot;--------------------&quot;);
            delay(500);
        }
        </code-block>
    </tab>
    <tab title="platformio.ini">
        <code-block lang="Plain Text">
            [env:esp32]
            platform = espressif32
            board = esp32dev
            framework = arduino
            monitor_speed = 115200
            build_flags = -D ESP32_BOARD
            &emsp;
            [env:esp8266]
            platform = espressif8266
            board = esp12e
            framework = arduino
            monitor_speed = 115200
            build_flags = -D ESP8266_BOARD
        </code-block>
    </tab>
</tabs>

> Se realizó un proyecto en PlatformIO para leer la concentración de $CO_2$ en el aire con el sensor MQ135, en la documentación se muestra los pasos para calibrar el sensor y el código para obtener la concentración de $CO_2$ en el aire.
> {style="note"}